# Maintainer: Aloxaf <aloxafx@gmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Frederic Bezies <fredbezies at gmail dot com>
# Contributor: Fernando Carmona Varo <ferkiwi @t gmail dot com>
# Contributor: Klemen Ko≈°ir <klemen913@gmail.com>

pkgname=cataclysm-dda-git
pkgver=0.E.r3394.g4f89139636
_pkgver=0.E
_offset=7382
pkgrel=1
pkgdesc="A post-apocalyptic roguelike."
#url="http://cataclysmrl.blogspot.com/"
#url="http://www.cataclysm.glyphgryph.com/"
#url="http://en.cataclysmdda.com/"
url="https://cataclysmdda.org/"
arch=('i686' 'x86_64')
license=("CCPL:by-sa")
conflicts=('cataclysm-dda' 'cataclysm-dda-ncurses' 'cataclysm-dda-tiles')
depends=('ncurses')
makedepends=('sdl2_image' 'sdl2_ttf' 'sdl2_mixer' 'freetype2' 'git' 'jq')
optdepends=('sdl2_image: for tiles'
            'sdl2_ttf: for tiles'
            'freetype2: for tiles'
            'sdl2_mixer: for tiles')
# source=("$pkgname"::'git://github.com/CleverRaven/Cataclysm-DDA.git#branch=master')
# The git repo is more than a GB
# so download a snapshot while waiting for shallow clone support in makepkg
source=("https://github.com/CleverRaven/Cataclysm-DDA/archive/master.zip")
md5sums=('SKIP')

pkgver() {
  local tag=$(curl --silent "https://api.github.com/repos/CleverRaven/Cataclysm-DDA/tags" | \
    jq --raw-output '[ .[] | select( .name | contains("cdda-jenkins-b") ) ] | .[0] | .name')
  local commit=$(curl --silent "https://api.github.com/repos/CleverRaven/Cataclysm-DDA/commits" | \
    jq --raw-output ".[0] | .sha")
  echo ${_pkgver}-$[${tag#*b}-${_offset}]-g${commit:0:10} | sed 's/-/.r/; s/-/./g'
}

build() {
  cd "$srcdir/Cataclysm-DDA-master"
  sed -iE 's/.*OTHERS \+= -ma.*/  OTHERS += -march=skylake/' Makefile
  # make PREFIX=/usr RELEASE=1 USE_XDG_DIR=1 LANGUAGES="zh_CN" \
  #   ASTYLE=0 LINTJSON=0 RUNTESTS=0 LTO=1
  make PREFIX=/usr RELEASE=1 USE_XDG_DIR=1 TILES=1 SOUND=1 LANGUAGES="zh_CN" \
    ASTYLE=0 LINTJSON=0 RUNTESTS=0 LTO=1 VERSION=$(sed 's/.r/-/; s/.g/-g/' <<< $pkgver)
  # LOCALIZE = 0   to save 30MB
  # DYNAMIC_LINKING = 1 ?

  # 'make install' needs this
  touch README.txt
}

package() {
  cd "$srcdir/Cataclysm-DDA-master"

  # no DESTDIR
  # make PREFIX="$pkgdir/usr" \
  #   RELEASE=1 LANGUAGES="zh_CN" \
  #   install

  make PREFIX="$pkgdir/usr" RELEASE=1 TILES=1 SOUND=1 LANGUAGES="zh_CN" \
    VERSION=$(sed 's/.r/-/; s/.g/-g/' <<< $pkgver) \
    install

  # Icon
  install -D 'build-data/osx/AppIcon.iconset/icon_128x128.png' "$pkgdir/usr/share/icons/hicolor/128x128/apps/cataclysm-dda.ico"

  # Docs
  install -d "$pkgdir/usr/share/doc/cataclysm-dda"
  cp -r doc/* "$pkgdir/usr/share/doc/cataclysm-dda"
  rm "$pkgdir/usr/share/doc/cataclysm-dda/"*.6
  install -Dm644 doc/cataclysm.6 "$pkgdir/usr/share/man/man6/cataclysm.6"
  install -Dm644 doc/cataclysm-tiles.6 "$pkgdir/usr/share/man/man6/cataclysm-tiles.6"

  # undo symlink
  rm "$pkgdir/usr/share/doc/cataclysm-dda/JSON_LOADING_ORDER.md"
  cp 'data/json/LOADING_ORDER.md' "$pkgdir/usr/share/doc/cataclysm-dda/JSON_LOADING_ORDER.md"

  # License
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
